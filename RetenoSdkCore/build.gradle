plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.vanniktech.maven.publish'
}
apply from: file("$project.rootDir/config.gradle")
apply from: "$project.rootDir/test_dependencies.gradle"

def sdkPropsFile = file("$project.rootDir/RetenoSdkCore/keys.properties")
Properties sdkProps = new Properties()
sdkProps.load(new FileInputStream(sdkPropsFile))

android {
    def globalConfig = rootProject.ext

    namespace 'com.reteno.core'
    compileSdk globalConfig.androidCompileSdkVersion

    defaultConfig {
        minSdk globalConfig.androidMinSdkVersion
        targetSdk globalConfig.androidTargetSdkVersion

        consumerProguardFiles "consumer-rules.pro"

        buildConfigField "String", "SQL_PASSWORD", sdkProps['sql.password']
        buildConfigField "String", "SDK_VERSION", "\"${project.VERSION_NAME}\""
    }

    buildTypes {
        debug {
            buildConfigField "String", "SENTRY_DSN", sdkProps['reteno.sentry.dsn.debug']

            minifyEnabled false
        }

        qa {
            buildConfigField "String", "SENTRY_DSN", sdkProps['reteno.sentry.dsn.qa']

            debuggable true
            minifyEnabled false
        }

        release {
            buildConfigField "String", "SENTRY_DSN", sdkProps['reteno.sentry.dsn.release']

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        debug {
            java.srcDirs = ['src/main/java', 'src/debug/java']
        }

        qa {
            java.srcDirs = ['src/main/java', 'src/debug/java']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation "io.sentry:sentry-android:6.4.3"

    implementation 'com.google.android.gms:play-services-base:18.1.0'
    implementation "com.google.android.gms:play-services-appset:16.0.2"

    implementation "androidx.core:core-ktx:1.9.0"
    compileOnly 'com.google.firebase:firebase-messaging-ktx:23.1.0'

    api 'com.google.code.gson:gson:2.9.1'

    implementation "net.zetetic:android-database-sqlcipher:4.5.2"
    implementation "androidx.sqlite:sqlite:2.1.0"

    implementation 'androidx.work:work-runtime-ktx:2.7.1'

    testDependencies()
}