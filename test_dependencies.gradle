def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

/******* Modules Dependencies *******/
ext.testDependencies = dependencyGroup {
    junit()
    mockk()
    powermock()
    robolectric()

    androidx()
    json()
    sqlCipher()
    workManager()

    testImplementation 'com.google.firebase:firebase-messaging-ktx:23.1.0'
}

/******* Dependency groups *******/

// Dependencies used for unit tests.
ext.junit = dependencyGroup {
    testImplementation 'junit:junit:4.13.2'
}

ext.mockk = dependencyGroup {
    testImplementation "io.mockk:mockk:1.13.2"
}

ext.powermock = dependencyGroup {
    def powermockVersion = '1.6.5'

    testImplementation "org.powermock:powermock-module-junit4:$powermockVersion"
    testImplementation "org.powermock:powermock-module-junit4-rule:$powermockVersion"
    testImplementation "org.powermock:powermock-api-mockito:$powermockVersion"
    testImplementation "org.powermock:powermock-classloading-xstream:$powermockVersion"
}

ext.robolectric = dependencyGroup {
    testImplementation('org.robolectric:robolectric:4.8.1') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testImplementation 'org.robolectric:shadows-play-services:3.3.2'

    testImplementation 'androidx.fragment:fragment-testing:1.4.1'
}

ext.androidx = dependencyGroup {
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
}

ext.json = dependencyGroup {
    testImplementation 'org.json:json:20180813'
}

ext.sqlCipher = dependencyGroup {
    testImplementation "net.zetetic:android-database-sqlcipher:4.5.2"
    testImplementation "androidx.sqlite:sqlite:2.2.0"
}

ext.workManager = dependencyGroup {
    implementation 'androidx.work:work-testing:2.7.1'
}